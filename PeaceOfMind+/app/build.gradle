buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "ca.mudar.fairphone.peaceofmind"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 18
        versionName "2.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs "en", "fr"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "int", "LOGGING_LEVEL", "6" // Log.ERROR
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
        }

        debug {
            applicationIdSuffix ".d"
            versionNameSuffix "-build-" + getDate()
            minifyEnabled false

            buildConfigField "int", "LOGGING_LEVEL", "2" // Log.VERBOSE
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }

        debugCrashlytics.initWith(buildTypes.debug)
        debugCrashlytics {
            applicationIdSuffix ""

            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
            matchingFallbacks = ['debug', 'release']
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def static getDate() {
    new Date().format('ddMMMyyyy.HHmm')
}

ext.supportLibraryVersion = '27.0.2'
ext.archComponentsVersion = '1.1.0'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation "android.arch.lifecycle:extensions:$archComponentsVersion"
    implementation "android.arch.lifecycle:common-java8:$archComponentsVersion"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.6'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // Kotlin databinding
    kapt 'com.android.databinding:compiler:3.0.1'

    // http://square.github.io/otto/
    implementation 'com.squareup:otto:1.3.8'

    // https://github.com/mikepenz/AboutLibraries
    implementation('com.mikepenz:aboutlibraries:6.0.3@aar') {
        transitive = true
    }

    // https://github.com/mudar/SeekArc - forked from github.com/neild001/SeekArc
    implementation 'com.github.mudar:SeekArc:v1.1.2'

    // https://github.com/Stericson/RootShell
    implementation project(':RootShell')

    // https://fabric.io/kits/android/crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}
